{
  alsa-lib,
  buildGoModule,
  ffmpeg-headless,
  flac,
  lib,
  libogg,
  libopenmpt,
  libvorbis,
  makeWrapper,
  mpg123,
  pkg-config,
  version ? "dev",
  yt-dlp,
}:

buildGoModule {
  pname = "cliamp";
  inherit version;

  src = lib.cleanSource ../.;
  vendorHash = "sha256-/c2MOMnG8twpr2/9plFanXkJwoIYNwC0mPksTklIcRw=";

  nativeBuildInputs = [
    makeWrapper
    pkg-config
  ];

  buildInputs = [
    alsa-lib
    flac
    libogg
    libvorbis
    mpg123
  ];

  ldflags = [
    "-s"
    "-w"
    "-X=main.version=${version}"
  ];

  postInstall = ''
    wrapProgram "$out/bin/cliamp" \
      --prefix PATH : ${lib.makeBinPath [
        ffmpeg-headless
        yt-dlp
      ]} \
      --prefix LD_LIBRARY_PATH : ${lib.makeLibraryPath [
        libopenmpt
      ]}
    install -Dm644 Cliamp.png "$out/share/icons/hicolor/512x512/apps/cliamp.png"
    install -Dm644 cliamp.desktop "$out/share/applications/cliamp.desktop"
  '';

  meta = {
    description = "Retro terminal music player inspired by Winamp";
    homepage = "https://github.com/bjarneo/cliamp";
    license = lib.licenses.mit;
    mainProgram = "cliamp";
    platforms = lib.platforms.linux;
  };
}
